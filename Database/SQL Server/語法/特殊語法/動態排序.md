# 動態排序

## 範例一

這個做法的缺點

-   如果 OrderBy 的欄位有包含了 `bigint` 跟 `DateTime2` 就會出現錯誤 `運算元類型衝突: bigint 與 date 不相容` ，可以改用 [範例二](#範例二) 的方式處理

```sql
SELECT *
FROM (
         SELECT ROW_NUMBER() OVER (ORDER BY [wso].[Id] ASC) AS [No],
                [wso].[Id],
                [wso].[Guid],
                [wso].[ApplicationNo],
                [v].[ChineseName]                           AS [VendorName],
                [wso].[AssignEngineerGuid],
                [engineer].[Name]                           AS [EngineerName],
                [wso].[AssignEngineerDate],
                [wso].[EngineerAssignDateStart],
                [wso].[EngineerAssignDateEnd],
                [wso].[AssignEngineerStatusId],
                [aes].[Name]                                AS [AssignEngineerStatus],
                [wso].[CreateTime],
                [wso].[UpdateTime],
                [wsos].[Name]                               AS [WorkServiceOrderStatus]
         FROM [dbo].[WorkServiceOrder] [wso]
             JOIN [dbo].[DataStatus] [ds]
                  ON [wso].[DataStatusId] = [ds].[Id]
                      AND [ds].[Value] = @DataStatusValue
             JOIN [dbo].[WorkServiceOrderStatus] [wsos]
                  ON [wso].[WorkServiceOrderStatusId] = [wsos].[Id]
             JOIN [dbo].[Vendor] [v]
                  ON [wso].[VendorGuid] = [v].[Guid]
             LEFT JOIN [dbo].[User] [engineer]
                       ON [wso].[AssignEngineerGuid] = [engineer].[Guid]
             JOIN [AssignEngineerStatus] [aes]
                  ON [wso].[AssignEngineerStatusId] = [aes].[Id]
         WHERE (@SearchApplicationNo IS NULL OR [wso].[ApplicationNo] LIKE '%' + @SearchApplicationNo + '%')
             --   AND (@SearchRequester IS NULL OR [wso].[Requester] LIKE '%' + @SearchRequester + '%')
           AND (@SearchAssignEngineerStatusId IS NULL OR [wso].[AssignEngineerStatusId] = @SearchAssignEngineerStatusId)
           AND (@SearchVendorName IS NULL OR (
                 [v].[ChineseName] LIKE '%' + @SearchVendorName + '%'
                 OR [v].[EnglishName] LIKE '%' + @SearchVendorName + '%'
             ))
     ) [query]
ORDER BY CASE @SortColumnOrder
             WHEN 'asc' THEN
                 CASE @SortColumn
                     WHEN 'No'                      THEN [query].[No]
                     WHEN 'ApplicationNo'           THEN [query].[ApplicationNo]
                     WHEN 'VendorName'              THEN [query].[VendorName]
                     WHEN 'EngineerName'            THEN [query].[EngineerName]
                     WHEN 'AssignEngineerDate'      THEN [query].[AssignEngineerDate]
                     WHEN 'EngineerAssignDateStart' THEN [query].[EngineerAssignDateStart]
                     WHEN 'EngineerAssignDateEnd'   THEN [query].[EngineerAssignDateEnd]
                     WHEN 'AssignEngineerStatus'    THEN [query].[AssignEngineerStatus]
                     END
             END ASC,
         CASE @SortColumnOrder
             WHEN 'desc' THEN
                 CASE @SortColumn
                     WHEN 'No'                      THEN [query].[No]
                     WHEN 'ApplicationNo'           THEN [query].[ApplicationNo]
                     WHEN 'VendorName'              THEN [query].[VendorName]
                     WHEN 'EngineerName'            THEN [query].[EngineerName]
                     WHEN 'AssignEngineerDate'      THEN [query].[AssignEngineerDate]
                     WHEN 'EngineerAssignDateStart' THEN [query].[EngineerAssignDateStart]
                     WHEN 'EngineerAssignDateEnd'   THEN [query].[EngineerAssignDateEnd]
                     WHEN 'AssignEngineerStatus'    THEN [query].[AssignEngineerStatus]
                     END
             END DESC
OFFSET @offset ROWS FETCH FIRST @onePageCount ROWS ONLY;
```

## 範例二

```sql
SELECT *
FROM (
         SELECT ROW_NUMBER() OVER (ORDER BY [wso].[Id] ASC) AS [No],
                [wso].[Id],
                [wso].[Guid],
                [wso].[ApplicationNo],
                [v].[ChineseName]                           AS [VendorName],
                [wso].[AssignEngineerGuid],
                [engineer].[Name]                           AS [EngineerName],
                [wso].[AssignEngineerDate],
                [wso].[EngineerAssignDateStart],
                [wso].[EngineerAssignDateEnd],
                [wso].[AssignEngineerStatusId],
                [aes].[Name]                                AS [AssignEngineerStatus],
                [wso].[CreateTime],
                [wso].[UpdateTime],
                [wsos].[Name]                               AS [WorkServiceOrderStatus]
         FROM [dbo].[WorkServiceOrder] [wso]
             JOIN [dbo].[DataStatus] [ds]
                  ON [wso].[DataStatusId] = [ds].[Id]
                      AND [ds].[Value] = @DataStatusValue
             JOIN [dbo].[WorkServiceOrderStatus] [wsos]
                  ON [wso].[WorkServiceOrderStatusId] = [wsos].[Id]
             JOIN [dbo].[Vendor] [v]
                  ON [wso].[VendorGuid] = [v].[Guid]
             LEFT JOIN [dbo].[User] [engineer]
                       ON [wso].[AssignEngineerGuid] = [engineer].[Guid]
             JOIN [AssignEngineerStatus] [aes]
                  ON [wso].[AssignEngineerStatusId] = [aes].[Id]
         WHERE (@SearchApplicationNo IS NULL OR [wso].[ApplicationNo] LIKE '%' + @SearchApplicationNo + '%')
             --   AND (@SearchRequester IS NULL OR [wso].[Requester] LIKE '%' + @SearchRequester + '%')
           AND (@SearchAssignEngineerStatusId IS NULL OR [wso].[AssignEngineerStatusId] = @SearchAssignEngineerStatusId)
           AND (@SearchVendorName IS NULL OR (
                 [v].[ChineseName] LIKE '%' + @SearchVendorName + '%'
                 OR [v].[EnglishName] LIKE '%' + @SearchVendorName + '%'
             ))
     ) [query]
ORDER BY CASE WHEN @SortColumn = 'No Asc' THEN [query].[No] END ASC,
         CASE WHEN @SortColumn = 'No Desc' THEN [query].[No] END DESC,
         CASE WHEN @SortColumn = 'ApplicationNo Asc' THEN [query].[ApplicationNo] END ASC,
         CASE WHEN @SortColumn = 'ApplicationNo Desc' THEN [query].[ApplicationNo] END DESC,
         CASE WHEN @SortColumn = 'VendorName Asc' THEN [query].[VendorName] END ASC,
         CASE WHEN @SortColumn = 'VendorName Desc' THEN [query].[VendorName] END DESC,
         CASE WHEN @SortColumn = 'EngineerName Asc' THEN [query].[EngineerName] END ASC,
         CASE WHEN @SortColumn = 'EngineerName Desc' THEN [query].[EngineerName] END DESC,
         CASE WHEN @SortColumn = 'AssignEngineerDate Asc' THEN [query].[AssignEngineerDate] END ASC,
         CASE WHEN @SortColumn = 'AssignEngineerDate Desc' THEN [query].[AssignEngineerDate] END DESC,
         CASE WHEN @SortColumn = 'EngineerAssignDateStart Asc' THEN [query].[EngineerAssignDateStart] END ASC,
         CASE WHEN @SortColumn = 'EngineerAssignDateStart Desc' THEN [query].[EngineerAssignDateStart] END DESC,
         CASE WHEN @SortColumn = 'EngineerAssignDateEnd Asc' THEN [query].[EngineerAssignDateEnd] END ASC,
         CASE WHEN @SortColumn = 'EngineerAssignDateEnd Desc' THEN [query].[EngineerAssignDateEnd] END DESC,
         CASE WHEN @SortColumn = 'AssignEngineerStatus Asc' THEN [query].[AssignEngineerStatus] END ASC,
         CASE WHEN @SortColumn = 'AssignEngineerStatus Desc' THEN [query].[AssignEngineerStatus] END DESC
OFFSET @offset ROWS FETCH FIRST @onePageCount ROWS ONLY;
```
